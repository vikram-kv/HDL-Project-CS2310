/***
* CHIP Name : HOCarryGen - Generates c(4i) for i=1,2,3,4 from the Level 0 propagate and generate terms by forming L1 propagate and generate terms.
* INPUT : c0 - carry into LSB. p[16] - Bus with L0 propagate terms and g[16] - Bus with L0 Generate terms
* OUTPUT : c(4i) for i = 1,2,3,4
* COMMENTS : The L1 propagate term is P(i) = p(4i).p(4i+1).p(4i+2).p(4i+3) where p(k) denotes the L0 propagate term of the kth bit.
*            The L1 generate term is G(i) = p(4i+3).p(4i+2).p(4i+1).g(4i) + p(4i+3).p(4i+2).g(4i+1) + p(4i+3).g(4i+2) + g(4i+3) where g(k) is the L0 generate term of the kth bit.
*	     Then, we have c(4i) = P(i-1).c(4i-4) + G(i-1) , by which c4,c8,c12,c16 can be found in terms of c0 only.
*****/


CHIP HOCarryGen {

	IN c0,p[16],g[16];
	OUT c4,c8,c12,c16;
PARTS:
	//Determination of L1 propagate terms
	Andf4(a=p[3],b=p[2],c=p[1],d=p[0],out=P0);
	Andf4(a=p[7],b=p[6],c=p[5],d=p[4],out=P1);
	Andf4(a=p[11],b=p[10],c=p[9],d=p[8],out=P2);
	Andf4(a=p[15],b=p[14],c=p[13],d=p[12],out=P3);

	//Determination of L1 G0
	Andf4(a=p[3],b=p[2],c=p[1],d=g[0],out=p3p2p1g0);
	Andf3(a=p[3],b=p[2],c=g[1],out=p3p2g1);
	And(a=p[3],b=g[2],out=p3g2);
	Orf4(a=p3p2p1g0,b=p3p2g1,c=p3g2,d=g[3],out=G0);

	//Determination of L1 G1
	Andf4(a=p[7],b=p[6],c=p[5],d=g[4],out=p7p6p5g4);
	Andf3(a=p[7],b=p[6],c=g[5],out=p7p6g5);
	And(a=p[7],b=g[6],out=p7g6);
	Orf4(a=p7p6p5g4,b=p7p6g5,c=p7g6,d=g[7],out=G1);

	//Determination of L1 G2
	Andf4(a=p[11],b=p[10],c=p[9],d=g[8],out=p11p10p9g8);
	Andf3(a=p[11],b=p[10],c=g[9],out=p11p10g9);
	And(a=p[11],b=g[10],out=p11g10);
	Orf4(a=p11p10p9g8,b=p11p10g9,c=p11g10,d=g[11],out=G2);

	//Determination of L1 G3
	Andf4(a=p[15],b=p[14],c=p[13],d=g[12],out=p15p14p13g12);
	Andf3(a=p[15],b=p[14],c=g[13],out=p15p14g13);
	And(a=p[15],b=g[14],out=p15g14);
	Orf4(a=p15p14p13g12,b=p15p14g13,c=p15g14,d=g[15],out=G3);

	//Determination of c4
	And(a=P0,b=c0,out=P0c0);
	Or(a=P0c0,b=G0,out=c4);

	//Determination of c8
	Andf3(a=P1,b=P0,c=c0,out=P1P0c0);
	And(a=P1,b=G0,out=P1G0);
	Orf3(a=P1P0c0,b=P1G0,c=G1,out=c8);

	//Determination of c12
	Andf4(a=P2,b=P1,c=P0,d=c0,out=P2P1P0c0);
	Andf3(a=P2,b=P1,c=G0,out=P2P1G0);
	And(a=P2,b=G1,out=P2G1);
	Orf4(a=P2P1P0c0,b=P2P1G0,c=P2G1,d=G2,out=c12);

	//Determination of c16
	Andf5(a=P3,b=P2,c=P1,d=P0,e=c0,out=P3P2P1P0c0);
	Andf4(a=P3,b=P2,c=P1,d=G0,out=P3P2P1G0);
	Andf3(a=P3,b=P2,c=G1,out=P3P2G1);
	And(a=P3,b=G2,out=P3G2);
	Orf5(a=P3P2P1P0c0,b=P3P2P1G0,c=P3P2G1,d=P3G2,e=G3,out=c16);
}
